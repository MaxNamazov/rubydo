App.controller 'ProjectsCtrl', ['$scope', '$rootScope', 'Projects', 'Tasks', 'Auth', '$modal', ($scope, $rootScope, Projects, Tasks, Auth, $modal)->
  $scope.projects = []  
  
  $scope.init = ->    
    Auth.currentUser().then(
      (user)->
        $rootScope.user = user        
        $scope.find()
      ,
      (error)->
        console.info error
    )
  $scope.completedTasks = ->
    completed = 0
    completed+=1 for task in this.project.tasks when task.completed
    return completed
          

  $scope.find = ->   
    Projects.query (projects)->
      $scope.projects = projects    

  $scope.changePosition = (id, position)->    
    Tasks.changePosition
      project_id: this.project.id
      task_id: id
      new_position: position

  $scope.updateTask = ->    
    Tasks.update
      project_id: this.$parent.project.id
      task_id: this.task.id
      task: this.task
    this.editMode = false

  $scope.addTask = (title)->  
    self = this    
    task = new Tasks 
      project_id: this.project.id
      name: title
    task.$save (res)->
      if res.status == 'created'
        self.project.tasks.push res.task
    self.newTitle = ''

  $scope.addProject = (title)->
    project = new Projects name: title
    project.$save (res)->
      $scope.find() if res.$resolved
      $scope.newProjectName = ''

  $scope.deleteProject = (idx)->    
    Projects.delete {project_id: this.project.id}, (res)->
      $scope.find() if res.$resolved 

  $scope.deleteTask = (idx)->    
    Tasks.delete {project_id: this.$parent.project.id, task_id: this.task.id}, (res)->
      $scope.find() if res.$resolved       

]